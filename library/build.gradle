apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

def GROUP_ID = 'org.im97mori'
def ARTIFACT_ID = 'rbt'
def VERSION_NAME = '0.1.0'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    api 'org.im97mori:ad:1.2.0'
    implementation 'org.im97mori:stacklog:0.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION_NAME

            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
        }
    }
    repositories {
        maven {
            url System.getenv('MAVEN_REPOSITORY_URL')
        }
    }
}

publish {
    dependsOn ':library:assembleRelease'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getenv('MAVEN_REPOSITORY_URL'))
            pom.groupId = GROUP_ID
            pom.artifactId = ARTIFACT_ID
            pom.version = VERSION_NAME

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'stacklog'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}

uploadArchives {
    dependsOn ':library:publish'
}